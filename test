#!/bin/tcsh -f

gcc -g -Wall -o fraggen fraggen.c

set fails=0
set matches=0

foreach frag ( `ls -1 kickc/fragment/*.asm | sed -e  's,^.*/,,g' -e 's/.asm$//g'` )
  echo -n $frag " : "
  ./fraggen $frag > myfrag
  sort < myfrag > s1
  sort < kickc/fragment/${frag}.asm > s2
  set sdiffs=`diff -w s1 s2 | wc -l`
  set diffs=`diff -w myfrag kickc/fragment/${frag}.asm | wc -l`
#  echo "diff: ${diffs} ${sdiffs}"
  if ( $diffs != 0 ) then
    if ( $sdiffs != 0 ) then
      echo "Fail"
      @ fails++
      diff myfrag kickc/fragment/${frag}.asm
    else
      echo "Instruction order differs"
      @ matches++
    endif
  else
    echo "Matches"
    @ matches++
  endif
end

echo ${fails} "tests failed."
echo ${matches} "tests passed."
